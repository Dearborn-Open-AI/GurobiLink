cmake_minimum_required(VERSION 3.15)

if(POLICY CMP0091)
	cmake_policy(SET CMP0091 NEW)
endif()

project(GUROBILink)

string(TIMESTAMP BUILD_TIME "%d %b %Y at %H:%M")

set(GUROBI_VERSION "9.0.2" CACHE STRING "GUROBI version")
string(REGEX REPLACE "([0-9]+)\\.[0-9]+\\.[0-9]+.*" "\\1" MAJ_VER ${GUROBI_VERSION})
set(GUROBI_MAJOR_VERSION ${MAJ_VER} CACHE STRING "Gurobi major version number" FORCE)
string(REGEX REPLACE "[0-9]+\\.([0-9]+)\\.[0-9]+.*" "\\1" MIN_VER ${GUROBI_VERSION})
set(GUROBI_MINOR_VERSION ${MIN_VER} CACHE STRING "Gurobi major version number" FORCE)

set(GUROBI_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/Libraries/Gurobi/${GUROBI_VERSION}" CACHE PATH "GUROBI installation")

set(INSTALL_PDB OFF CACHE BOOL "Include PDB file in paclet layout")

set(PACLET_VERSION 1.0.1 CACHE STRING "Paclet version")
set(TARGET_WOLFRAM_VERSION 12.3+ CACHE STRING "Target Mathematica version")

if(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
  set(CMAKE_INSTALL_PREFIX "${PROJECT_BINARY_DIR}/install" CACHE PATH "Install prefix" FORCE)
endif()

set(GUROBI_DLL
	${CMAKE_SHARED_LIBRARY_PREFIX}gurobi${GUROBI_MAJOR_VERSION}${GUROBI_MINOR_VERSION}${CMAKE_SHARED_LIBRARY_SUFFIX}
)
set(GUROBI_LIB ${GUROBI_DLL})

if(NOT CMAKE_SIZEOF_VOID_P EQUAL 8)
	message(FATAL_ERROR "Only 64-bit platforms are supported by ${PROJECT_NAME}")
endif()

if(WIN32)
	set(DEFAULT_SYSTEMID Windows-x86-64)
	set(DEFAULT_PLATFORM win64)
	set(QUALIFIER Win64)
	string(REPLACE "${CMAKE_SHARED_LIBRARY_SUFFIX}" "${CMAKE_IMPORT_LIBRARY_SUFFIX}" GUROBI_LIB ${GUROBI_DLL})
	set(MATHEMATICA_LAYOUT "C:/Program Files/Wolfram Research/Mathematica/12.3" CACHE PATH "Mathematica installation")
elseif(APPLE)
	if(CMAKE_OSX_ARCHITECTURES STREQUAL "x86_64")
		set(DEFAULT_SYSTEMID MacOSX-x86-64)
	elseif(CMAKE_OSX_ARCHITECTURES MATCHES "^arm64e?$")
		set(DEFAULT_SYSTEMID MacOSX-ARM64)
	elseif(CMAKE_SYSTEM_PROCESSOR STREQUAL "x86_64")
		set(DEFAULT_SYSTEMID MacOSX-x86-64)
	elseif(CMAKE_SYSTEM_PROCESSOR MATCHES "arm64")
		set(DEFAULT_SYSTEMID MacOSX-ARM64)
	endif()
	if(DEFAULT_SYSTEMID STREQUAL "MacOSX-x86-64")
		set(DEFAULT_PLATFORM mac64)
	else()
		set(DEFAULT_PLATFORM macos_universal2)
		if(GUROBI_VERSION VERSION_LESS 9.1)
			message(FATAL_ERROR "MacOSX-ARM64 is not supported by Gurobi ${GUROBI_VERSION}")
		endif()
	endif()
	set(QUALIFIER Mac64)
	set(CMAKE_BUILD_WITH_INSTALL_RPATH ON)
	set(MATHEMATICA_LAYOUT "/Applications/Mathematica.app/Contents" CACHE PATH "Mathematica installation")
elseif(UNIX)
	if(CMAKE_SYSTEM_PROCESSOR MATCHES "arm")
		message(FATAL_ERROR "Linux-ARM64 is not supported by ${PROJECT_NAME}")
	endif()
	set(DEFAULT_SYSTEMID Linux-x86-64)
	set(DEFAULT_PLATFORM linux64)
	set(QUALIFIER Lin64)
	set(CMAKE_INSTALL_RPATH "\$ORIGIN")
	set(MATHEMATICA_LAYOUT "/usr/local/Wolfram/Mathematica/12.3" CACHE PATH "Mathematica installation")
endif()

set(SYSTEMID ${DEFAULT_SYSTEMID} CACHE STRING "Machine SystemID")
set(PLATFORM ${DEFAULT_PLATFORM} CACHE STRING "Gurobi platform string")

if(WIN32)
	set(GUROBI_DLL_PATH "${GUROBI_ROOT}/${PLATFORM}/bin")
else()
	set(GUROBI_DLL_PATH "${GUROBI_ROOT}/${PLATFORM}/lib")
endif()
set(GUROBI_LIBRARY_PATH "${GUROBI_ROOT}/${PLATFORM}/lib")
set(GUROBI_INCLUDE_PATH "${GUROBI_ROOT}/${PLATFORM}/include")

if(NOT WOLFRAM_INCLUDE_PATH)
	set(WOLFRAM_INCLUDE_PATH "${MATHEMATICA_LAYOUT}/SystemFiles/IncludeFiles/C")
endif()

message(STATUS "WOLFRAM_INCLUDE_PATH: ${WOLFRAM_INCLUDE_PATH}")
message(STATUS "GUROBI_INCLUDE_PATH: ${GUROBI_INCLUDE_PATH}")
message(STATUS "GUROBI_LIBRARY_PATH: ${GUROBI_LIBRARY_PATH}")

find_file(WOLFRAM_LIBRARY_HEADER "WolframLibrary.h" PATHS ${WOLFRAM_INCLUDE_PATH} REQUIRED NO_DEFAULT_PATH)
find_file(GUROBI_C_HEADER "gurobi_c.h" PATHS ${GUROBI_INCLUDE_PATH} REQUIRED NO_DEFAULT_PATH)
find_file(GUROBI_LIBRARY ${GUROBI_LIB} PATHS ${GUROBI_LIBRARY_PATH} REQUIRED NO_DEFAULT_PATH)

set(GUROBILINK_SOURCES
	CSource/GUROBILink.cpp
	CSource/GUROBISolution.cpp
)

set(PACLET_DESTINATION ${CMAKE_INSTALL_PREFIX}/${FULL} CACHE PATH "Full build destination")
set(PACLET_LIBDIR ${CMAKE_INSTALL_PREFIX}/${PROJECT_NAME}/LibraryResources/${SYSTEMID})

add_library(${PROJECT_NAME} SHARED ${GUROBILINK_SOURCES})
set_target_properties(${PROJECT_NAME} PROPERTIES
	MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>"
	PREFIX ""
)
target_include_directories(${PROJECT_NAME} PRIVATE
	${CMAKE_SOURCE_DIR}/CSource
	${GUROBI_INCLUDE_PATH}
	${WOLFRAM_INCLUDE_PATH}
)
target_link_directories(${PROJECT_NAME} PRIVATE ${GUROBI_LIBRARY_PATH})
target_link_libraries(${PROJECT_NAME} PRIVATE ${GUROBI_LIB})
target_compile_definitions(${PROJECT_NAME} PRIVATE "$<$<CONFIG:DEBUG>:DEBUG>")

if(APPLE)
	execute_process(
			COMMAND otool -D "${GUROBI_ROOT}/${PLATFORM}/lib/${GUROBI_LIB}"
			OUTPUT_VARIABLE OTOOL_OUT
	)
	string(REGEX REPLACE "^.*:.|.$" "" GUROBI_LIB_INSTALL_NAME ${OTOOL_OUT})
	add_custom_command(TARGET ${PROJECT_NAME} POST_BUILD
		COMMAND install_name_tool -change ${GUROBI_LIB_INSTALL_NAME} @loader_path/${GUROBI_LIB} $<TARGET_FILE_NAME:${PROJECT_NAME}>
		COMMENT "Setting rpath for ${GUROBI_LIB}"
	)
endif()

install(TARGETS ${PROJECT_NAME}
		LIBRARY DESTINATION ${PACLET_LIBDIR}
		RUNTIME DESTINATION ${PACLET_LIBDIR}
)

install(FILES
	${GUROBI_DLL_PATH}/${GUROBI_DLL}
	DESTINATION ${PACLET_LIBDIR}
)

if(INSTALL_PDB)
	install(FILES $<TARGET_PDB_FILE:${PROJECT_NAME}> DESTINATION ${PACLET_LIBDIR} OPTIONAL)
endif()

configure_file(
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}/PacletInfo.m.in
	${CMAKE_BINARY_DIR}/generated/PacletInfo.m
)

set(WL_SOURCES
	${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}/${PROJECT_NAME}.m
	${CMAKE_BINARY_DIR}/generated/PacletInfo.m
)

install(FILES ${WL_SOURCES} DESTINATION ${CMAKE_INSTALL_PREFIX}/${PROJECT_NAME})